//===== rAthena Script ======================================= 
//= Comodo Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Comodo.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Fixed and optimized. [Euphy]
//= 1.2 Navigation system update. [Euphy]
//============================================================ 

comodo,322,178,4	script	Comodo Guide#01comodo::GuideComodo	700,{
	mes "[Comodo 解說員]";
	mes "Welcome to ^8B4513Comodo^000000,";
	mes "the Beach Town of the Rune-Midgarts Kingdom.";
	mes "Do you need help navigating the town?";
	F_Navi("[Comodo Guide]");
	next;
	while (1) {
		switch(select("[ 主要設施 ]:[ 商人&小幫手 ]:刪除小地圖的標記:Cancel:About ^8B4513Comodo^000000")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 酒店 ]:[ Casino ]:[ 武器、防具店 ]:[ 道具店 ]:[ Campsite ]:[ Souvenir Vendor ]:[ East Cave Mao ]:[ West Cave Karu ]:[ North Cave Ruanda ]:[ Hula Dance Stage ]:[ Kafra Co. Western Branch ]:返回上一選單")) {
				case 1:
					callsub L_Mark, F_Navi("酒店","comodo,161,315","^FF0000");
					viewpoint 1,166,298,0,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("Casino","comodo,140,111","^FFA500");
					viewpoint 1,140,98,1,0xFFA500;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("武器、防具店","comodo,266,74","^FF4500");
					viewpoint 1,266,70,2,0xFF4500;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("道具店","comodo,92,128","^B8860B");
					viewpoint 1,86,128,3,0xB8860B;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Campsite","comodo,209,308","^32CD32");
					viewpoint 1,210,308,4,0x32CD32;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("Souvenir Vendor","comodo,293,125","^6B8E23");
					viewpoint 1,298,124,5,0x6B8E23;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("East Cave Mao","comodo,333,175","^008B8B");
					viewpoint 1,333,175,6,0x008B8B;
					next;
					break;
				case 8:
					callsub L_Mark, F_Navi("West Cave Karu","comodo,24,214","^1E90FF");
					viewpoint 1,24,214,7,0x1E90FF;
					next;
					break;
				case 9:
					callsub L_Mark, F_Navi("North Cave Ruanda","comodo,176,358","^0000CD");
					viewpoint 1,176,358,8,0x0000CD;
					next;
					break;
				case 10:
					callsub L_Mark, F_Navi("Hula Dance Stage","comodo,188,168","^C71585");
					viewpoint 1,188,168,9,0xC71585;
					next;
					break;
				case 11:
					mes "[Comodo 解說員]";
					mes "Let me mark the location of the";
					mes F_Navi("Kafra Corporation Western Branch","comodo,131,195","^DB7093");
					mes "小地圖上了，";
					mes "還需要其它服務嗎?";
					viewpoint 1,136,202,10,0xDB7093;
					next;
					break;
				case 12:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Chief's House ]:[ Bor Robin ]:[ 卡普拉職員 ]:[ Roaming Bard ]:返回上一選單")) {
				case 1:
					callsub L_Mark, F_Navi("Chief's House","comodo,115,290","^6495ED");
					viewpoint 1,114,294,11,0x6495ED;
					next;
					break;
				case 2:
					mes "[Comodo 解說員]";
					mes F_Navi("Bor Robin","comodo,193,151","^228B22");
					mes "helps adventurers to become ^228B22Dancers^000000.";
					mes "還需要其它服務嗎?";
					viewpoint 1,193,151,12,0x228B22;
					next;
					break;
				case 3:
					callsub L_Mark,"^1E90FF卡普拉職員^000000";
					viewpoint 1,195,149,13,0x1E90FF;
					next;
					break;
				case 4:
					mes "[Comodo 解說員]";
					mes "The "+F_Navi("Roaming Bard","comodo,211,155","^DB7093");
					mes "helps adventurers to become ^DB7093Bards^000000.";
					mes "還需要其它服務嗎?";
					viewpoint 1,211,155,14,0xDB7093;
					next;
					break;
				case 5:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Comodo 解說員]";
			mes "已刪除小地圖上的標誌了，";
			mes "還需要其它服務嗎?";
			viewpoint 2,166,298,0,0xFFFFFF;
			viewpoint 2,140,98,1,0xFFFFFF;
			viewpoint 2,266,70,2,0xFFFFFF;
			viewpoint 2,86,128,3,0xFFFFFF;
			viewpoint 2,210,308,4,0xFFFFFF;
			viewpoint 2,298,124,5,0xFFFFFF;
			viewpoint 2,333,175,6,0xFFFFFF;
			viewpoint 2,24,214,7,0xFFFFFF;
			viewpoint 2,176,358,8,0xFFFFFF;
			viewpoint 2,188,168,9,0xFFFFFF;
			viewpoint 2,136,202,10,0xFFFFFF;
			viewpoint 2,114,294,11,0xFFFFFF;
			viewpoint 2,193,151,12,0xFFFFFF;
			viewpoint 2,195,149,13,0xFFFFFF;
			viewpoint 2,211,155,14,0xFFFFFF;
			next;
			break;
		case 4:
			mes "[Comodo 解說員]";
			mes "祝您旅途愉快!";
			close;
		case 5:
			mes "[Comodo 解說員]";
			mes "^8B4513Comodo^000000 is";
			mes "located inside a cave";
			mes "and can never be reached by natural sunlight.";
			next;
			mes "[Comodo 解說員]";
			mes "The villagers of ^8B4513Comodo^000000";
			mes "have overcome their poor natural surroundings";
			mes "to create a popular tourist site.";
			mes "還需要其它服務嗎?";
			next;
			break;
		}
	}
	end;
L_Mark:
	mes "[Comodo 解說員]";
	mes "Let me mark the location of";
	mes "the "+getarg(0);
	mes "小地圖上了，";
	mes "還需要其它服務嗎?";
	return;
}
comodo,181,347,4	duplicate(GuideComodo)	Comodo Guide#02comodo	700
comodo,197,149,3	duplicate(GuideComodo)	Comodo Guide#03comodo	700
comodo,37,219,4	duplicate(GuideComodo)	Comodo Guide#04comodo	700
