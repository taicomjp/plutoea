//===== rAthena Script ======================================= 
//= Soul Linker Job Quest
//===== By: ================================================== 
//= Celestria & Samuray22
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Job quest for Soul Linker class.
//= [Vars Used]
//= SOUL_Q = Job Change Phase. (Max 4)
//===== Additional Comments: ================================= 
//= 1.2 Rescripted to Aegis 10.3 standards. [Samuray22]
//= 1.3 Slight updates and fixes to the script. [L0ne_W0lf]
//=	Removes the use of labels, and corrects other errors.
//=	Removed the GM-use only NPC from the sec_in map. 
//=	No longer used a global var, uses an NPC var instead.
//= 1.3a Deleted Empty Color Tag. (bugreport:1572) [Samuray22]
//= 1.4 Replaced effect numerics with constants. [L0ne_W0lf]
//= 1.5 Added Quest Log commands. [Kisuka]
//============================================================ 

morocc_in,174,30,6	script	小孩子#link1	716,{
	if (Class == Job_Soul_Linker) {
		mes "[瑪雅]";
		mes "祝你旅遊愉快";
		mes "當你掌握更多靈魂連結技能";
		mes "你將更能夠獲得更多的靈魂力量";
		mes "去輔助你的隊友..";
		mes "";
		mes "";
		close;
	}
	if (Class == Job_Star_Gladiator) {
		mes "[小孩子]";
		if (Sex == 1) {
			mes "你不是一個太陽的戰士嗎?";
			mes "";
		}
		else {
			mes "你不是一個月亮的戰士嗎";
			mes "";
		}
		mes "用你的方式繼續戰鬥吧! ";
		mes "畢竟我們都是以跆拳為基礎的, 不是嗎";
		mes "";
		mes "";
		close;
	}
	if (Class != Job_Taekwon) {
		mes "[小孩子]";
		mes "摁? 我沒有什麼可以幫助你的";
		mes "但如果你知道有誰是跆拳道學得不錯的";
		mes "那可以把他介紹給我認識";
		mes "我可能可以幫助他";
		mes "";
		mes "";
		close;
	}
	if (JobLevel < 40) {
		mes "[小孩子]";
		mes "所以.. 你在學習跆拳道嗎?";
		mes "摁! 這樣非常好!";
		mes "繼續保持嚴格的磨練!";
		mes "增強你的技能與實力";
		mes "";
		close;
	}
	else if (JobLevel > 39) {
		if (SOUL_Q == 0) {
			mes "[小孩子]";
			mes "...";
			mes "嘿.";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "你叫我嗎?";
			next;
			mes "[小孩子]";
			mes "對! 我在叫你.";
			mes "別讓我這麼大聲講話";
			mes "過來我附近";
			mes "";
			next;
			if (select("你這個粗魯的孩子!:忽視.") == 1) {
				mes "[小孩子]";
				mes "你真幸運";
				mes "我很想和你聊聊!";
				mes "你可能會看我像個孩子一樣";
				mes "但我已經活了超過三百年!";
				mes "";
				emotion e_pif;
				next;
				mes "[小孩子]";
				mes "聽著...";
				mes "我知道你是一個跆拳家弟子";
				mes "這是一項受人尊敬的武藝";
				mes "但我有一個給你的建議";
				mes "如果你想聽的話..";
				mes "";
				emotion e_heh;
				next;
				mes "[小孩子]";
				mes "我正在看著你";
				mes "而且我可以告訴你, 你對於靈性這方面是非常有天份的";
				mes "你擁有很大的潛力, 我不想看到這樣的潛力被浪費掉";
				mes "為什麼你不考慮看看成為一個''悟靈師''?";
				mes "";
				mes "";
				next;
				if (select("哈 愚蠢的小男孩~:悟靈師?") == 1) {
					mes "[小孩子]";
					mes "你.. 你不相信我?";
					mes "你可以忘記我看起來像一個小男孩幾分鐘嗎?";
					mes "呵呵.. 青少年...";
					mes "";
					mes "";
					mes "";
					close;
				}
				mes "[小孩子]";
				mes "悟靈師連結那些雖然已經倒下但仍然希望為這世界奮戰戰士的靈魂";
				mes "";
				mes "";
				mes "";
				mes "";
				mes "";
				next;
				mes "[小孩子]";
				mes "部過, 既然你有靈性的天分";
				mes "這些靈魂將被你吸引";
				mes "只要你擁有足夠的訓練";
				mes "你將可以暫時將這些靈魂灌輸到隊友的身上";
				mes "藉以提供隊友的戰鬥能力.";
				next;
				mes "[小孩子]";
				mes "但你不能灌輸靈魂到你自己的身上";
				mes "此外, 取決於你的技能";
				mes "靈魂連結只能附加力量到某些職業身上";
				mes "";
				mes "";
				next;
				mes "[小孩子]";
				mes "你必須進入一個完全不同的世界";
				mes "成為悟靈師";
				mes "但我不能保證你一定會成為悟靈師";
				mes "所以你覺得呢?";
				mes "";
				next;
				if (select("不, 至少不是現在...:好吧, 我該做什麼?") == 1) {
					mes "[小孩子]";
					mes "阿, 好吧";
					mes "如果你以後決定要成為一個悟靈師";
					mes "再回來找我吧";
					mes "";
					mes "";
					close;
				}
				set SOUL_Q,1;
				setquest 6005;
				mes "[小孩子]";
				mes "所以你想要成為一個悟靈師?";
				mes "很好!";
				mes "首先我需要你去幫我收集幾樣物品回來";
				mes "不用擔心, 我會和你解釋為什麼需要他們";
				mes "";
				mes "";
				next;
				mes "[小孩子]";
				mes "現在, 去收集";
				mes "^0000FF1個 三克拉鑽石^000000,";
				mes "^0000FF1個 不死心臟^000000,";
				mes "^0000FF1個 永恆玫瑰^000000.";
				mes "別讓我等太久, 好嗎?";
				mes "";
				close;
			}
			mes "[小孩子]";
			mes "摁...?";
			mes "等等 你要去哪裡?";
			mes "我...";
			mes "我在和你說話!";
			close;
		}
		else if (SOUL_Q == 1) {
			if (Class == Job_Taekwon) {
				mes "[小孩子]";
				mes "你回來了?";
				mes "所以你帶來了";
				mes "^0000FF1個 三克拉鑽石^000000,";
				mes "^0000FF1個 不死心臟^000000,";
				mes "^0000FF1個 永恆玫瑰^000000.";
				mes "就像我要求你的那樣?";
				next;
				if (select("在這裡.:不, 還沒...") == 1) {
					if (countitem(732) > 0 && countitem(929) > 0 && countitem(748) > 0) {
						delitem 732,1; //Crystal_Jewel__
						delitem 929,1; //Immortal_Heart
						delitem 748,1; //Witherless_Rose
						set SOUL_Q,2;
						changequest 6005,6006;
						mes "[小孩子]";
						mes "很好, 我看到你帶來的東西了";
						mes "但是在我們開始之前";
						mes "先讓我自我介紹一下";
						mes "我叫做瑪雅, 我已經活超過三百年了";
						mes "";
						next;
						mes "[瑪雅]";
						mes "不要在乎太多細節";
						mes "我一直在負責招收更多的悟靈師";
						mes "這也是我一部分原因, 摁..你懂得..";
						mes "";
						next;
						mes "[瑪雅]";
						mes "不管怎樣, 我仍然需要準備一些材料";
						mes "所以你等等再來找我吧, 我們等等在聊";
						mes "";
						mes "";
						mes "";
						close;
					}
					mes "[小孩子]";
					mes "摁...?";
					mes "嘿. 你忘了一些東西";
					mes "現在再回去準備準備";
					mes "我需要的東西, 好嗎?";
					mes "";
					mes "";
					emotion e_pif;
					next;
					mes "[小孩子]";
					mes "我知道, 我已經告訴過你, 我們需要什麼了";
					mes "但我還是要再提醒你一次:";
					mes "^0000FF1個 三克拉鑽石^000000,";
					mes "^0000FF1個 不死心臟^000000,";
					mes "^0000FF1個 永恆玫瑰^000000.";
					mes "";
					close;
				}
				mes "[小孩子]";
				mes "摁. 好.";
				mes "雖然我有足夠的時間";
				mes "但我不喜歡等待..";
				mes "";
				mes "";
				close;
			}
			set SOUL_Q,0;
			mes "[小孩子]";
			mes "你已經成為一個太陽、月亮、星星的戰士了?";
			mes "我並不知道你有這樣的潛能";
			mes "我想我不能責怪你...";
			mes "";
			mes "";
			close;
		}
		else if (SOUL_Q == 2) {
			if (SkillPoint) {
				mes "[瑪雅]";
				mes "你仍然有一些沒有配置完的技能點數";
				mes "請先將將這些技能點數用來學習跆拳道的技能.";
				mes "然後回到這裡";
				mes "";
				mes "";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[瑪雅]";
				mes "現在, 還有其他人還沒完成";
				mes "悟靈師轉職儀式.";
				mes "請你等待, 直到儀式完成.";
				mes "等好了的時候, 我會通知你";
				mes "";
				mes "";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[瑪雅]";
			mes "太好了, 我已經完成各項準備工作";
			mes "現在我們將開始成為悟靈師的儀式";
			mes "請你閉上你的雙眼";
			mes "";
			mes "";
			mes "";
			close2;
			warp "job_soul",30,30;
			end;
		}
		else if (SOUL_Q > 2) {
			mes "[瑪雅]";
			mes "你準備好進入你內心的深處了嗎?";
			mes "";
			mes "";
			next;
			if (select("不:好") == 1) {
				mes "[瑪雅]";
				mes "好, 等你準備好再回來找我吧";
				mes "";
				mes "";
				mes "";
				mes "";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[瑪雅]";
				mes "現在, 還有其他人還沒完成";
				mes "悟靈師轉職儀式.";
				mes "請你等待, 直到儀式完成.";
				mes "等好了的時候, 我會通知你";
				mes "";
				mes "";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[瑪雅]";
			mes "那麼好吧, 閉上你的雙眼";
			mes "放鬆全身.";
			mes "我們將回到你內心的深處";
			mes "";
			close2;
			warp "job_soul",30,30;
			end;
		}
	}

OnInit:
	set .SoulLinkerTest,0;
	end;
}

job_soul,30,31,0	script	瑪雅#link2::SLTester	-1,3,3,{
OnTouch:
	if (Class == Job_Taekwon) {
		if (JobLevel < 40) {
			set getvariableofnpc(.SoulLinkerTest,"小孩子#link1"),0;
			mes "[瑪雅]";
			mes "摁? 你怎模來到這裡的";
			mes "你沒有那個資格..";
			mes "我帶你回到夢羅克吧...";
			close2;
			warp "morocc",157,47;
			end;
		}
		if (SOUL_Q == 2) {
			mes "[瑪雅]";
			mes "你認識這個地方..?";
			mes "現在我們將進入你的內心";
			mes "那些已故的戰士靈魂在這徘徊";
			mes "等著你呼喚他們的力量";
			mes "";
			mes "";
			next;
			mes "[瑪雅]";
			mes "現在, 只有這裡這幾個";
			mes "但如果你持續的訓練自己";
			mes "你將能夠呼喚更多的靈魂做為靈魂鏈結";
			mes "";
			mes "";
			next;
			set SOUL_Q,3;
			changequest 6006,6007;
			mes "[瑪雅]";
			mes "你只能在這裡停留三分鐘";
			mes "我建議你如果有機會的話和那些靈魂聊聊.";
			mes "";
			mes "";
			mes "";
			close;
		}
		else if (SOUL_Q == 3) {
			mes "[瑪雅]";
			mes "去聆聽那些靈魂的想法吧..";
			mes "因為他們在這裡不能移動...";
			mes "";
			mes "";
			mes "";
			close;
		}
		else if (SOUL_Q == 4) {
			mes "[瑪雅]";
			mes "我相信你已經準備好成為一個悟靈師了";
			mes "然而, 你可以繼續和靈魂說話.. 如果你想要的話";
			mes "";
			mes "";
			mes "";
			mes "";
			next;
			if (select("繼續和靈魂說話:成為一個悟靈師") == 1) {
				mes "[瑪雅]";
				mes "好吧, 盡量快一點";
				mes "因為我們能停留在這裡的時間有限";
				mes "雖然我們以後還可以再過來這裡... 如果你想要的話..";
				mes "";
				mes "";
				mes "";
				close;
			}
			mes "[瑪雅]";
			mes "那麼就讓我們開始一事吧";
			mes "這些物品將會用來賦予你借用力量的能力";
			mes "黨下的戰士將會把力量借助於你的戰友";
			mes "";
			mes "";
			mes "";
			next;
			mes "[瑪雅]";
			mes "而永和玫瑰將會讓你永不滅亡...";
			mes "";
			specialeffect EF_MAPPILLAR2,AREA,"瑪雅#link2";
			next;
			mes "[瑪雅]";
			mes "而永和玫瑰將會讓你永不滅亡...";
			mes "這不朽的心臟將會不斷的運輸血量";
			mes "";
			mes "";
			next;
			mes "[瑪雅]";
			mes "而永和玫瑰將會讓你永不滅亡...";
			mes "這不朽的心臟將會不斷的運輸血量...";
			mes "這鑽石在你發生致命的危機的時候將會為你化為灰燼";
			mes "";
			mes "";
			next;
			mes "[瑪雅]";
			mes "那些在死亡後還希望繼續奮戰的靈魂";
			mes "將會為你而戰! 利用你的能力吧!";
			mes "悟靈師";
			mes "在公平與正義的名義之下.";
			mes "";
			next;
			completequest 6008;
			callfunc "Job_Change",Job_Soul_Linker;
			callfunc "F_ClearJobVar";		// clears all job variables for the current player
			set SOUL_Q,0;
			mes "[瑪雅]";
			mes "祝你在未來的生活中能夠非常的順利";
			mes "在你身邊的戰友、靈魂將能夠保護你、幫助你戰鬥";
			mes "現在, 再見了. 朋友";
			mes "";
			mes "";
			close2;
			set getvariableofnpc(.SoulLinkerTest,"小孩子#link1"),0;
			donpcevent "Timer#link3::OnDisable";
			warp "morocc",157,47;
			end;
		}
		set getvariableofnpc(.SoulLinkerTest,"小孩子#link1"),0;
		mes "[瑪雅]";
		mes "摁...?";
		mes "這還不是你該來的地方";
		mes "讓我帶你回到夢羅克吧";
		mes "";
		mes "";
		close2;
		warp "morocc",157,47;
		end;
	}
	set getvariableofnpc(.SoulLinkerTest,"小孩子#link1"),0;
	if (Class == Job_Soul_Linker) {
		mes "[瑪雅]";
		mes "是時候了，你應該前往更廣闊的世界冒險";
		mes "悟靈師將會是反抗邪惡的一大利器...";
		mes "";
		mes "";
	}
	else {
		mes "[瑪雅]";
		mes "摁...?";
		mes "這還不是你該來的地方";
		mes "讓我帶你回到夢羅克吧";
		mes "";
	}
	close2;
	warp "morocc",157,47;
	end;
}

job_soul,35,30,6	duplicate(SLTester)	瑪雅#link6	716

job_soul,30,35,6	script	武道家的靈魂#link4	827,{
	if (SOUL_Q == 2) {
		mes "[武道家的靈魂]";
		mes "我.. 我是誰...?";
		mes "我想... 我想";
		mes "你最好先去找Maya聊聊...";
		mes "";
		mes "";
		mes "";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[武道家的靈魂]";
		mes "在活著的時候, 我的同伴們向我保證";
		mes "我將能完成所有的事情，就像我能成為一個武道家一樣";
		mes "但.. 我仍然不滿意我的能力..";
		mes "";
		mes "";
		mes "";
		next;
		mes "[武道家的靈魂]";
		mes "在死亡後, 我有很多的遺憾..";
		mes "我從未有機會成為一個偉大的武道家";
		mes "唯有將我的力量借給他人";
		mes "才能讓我成功.";
		mes "";
		mes "";
		next;
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		mes "[武道家的靈魂]";
		mes "我求求你...";
		mes "我真的需要你的幫助";
		mes "我已經充分瞭解了武道家的能力...";
		mes "";
		mes "";
		close;
	}
	mes "[武道家的靈魂]";
	mes "...";
	close;
}

job_soul,30,25,7	script	賢者的靈魂#link5	754,{
	if (SOUL_Q == 2) {
		mes "[賢者的靈魂]";
		mes "去找找Maya.";
		mes "我怕我會讓你疑惑..";
		mes "如果你沒先去找Maya的話..";
		mes "";
		mes "";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[賢者的靈魂]";
		mes "我對於知識的追求";
		mes "授予我難以置信的力量:";
		mes "在活著的時候,我可以摧毀任何我想要摧毀的東西";
		mes "只有少數的賢者能達到我的水準...";
		mes "";
		next;
		mes "[賢者的靈魂]";
		mes "我死了, 但我從來不能傳送訊息給那個世界";
		mes "我仍然想使用我的能力、我的知識";
		mes "去建立我想建立的東西、去摧毀我想摧毀的東西";
		mes "";
		mes "";
		next;
		mes "[賢者的靈魂]";
		mes "他是不夠的, 如果我能將我的力量";
		mes "借給其他賢者, 那是值得的";
		mes "但如果要做到這一點";
		mes "我必須借助你的力量";
		mes "我求求你";
		mes "讓我成為你的靈魂戰友";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[賢者的靈魂]";
		mes "我相信你是唯一可以幫助我的人..";
		mes "";
		mes "";
		mes "";
		close;
	}
	mes "[賢者的靈魂]";
	mes "...";
	close;
}

job_soul,25,30,5	script	煉金術師的靈魂#link7	744,{
	if (SOUL_Q == 2) {
		mes "[煉金術師的靈魂]";
		mes "噢! 我真想和你說話";
		mes "但我不能這麼做";
		mes "先去找Maya吧! 但我真的需要你的幫助";
		mes "";
		mes "";
		mes "";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[煉金術師的靈魂]";
		mes "沒有吹牛, 我是最迅速的煉金術師";
		mes "事實上我甚至可以永遠當最快的煉金術師";
		mes "但是後來我慢慢傲慢.. 並再一次意外中奪走性命了..";
		mes "";
		mes "";
		mes "";
		next;
		mes "[煉金術師的靈魂]";
		mes "但死亡不會扼殺我的能力";
		mes "事實上我甚至在這裡提升了我的能力";
		mes "但我已經離開了人世..";
		mes "我不能往未來前進";
		mes "直到我透過我的能力...";
		mes "";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[煉金術師的靈魂]";
		mes "做為一個靈魂, 我無能為力,";
		mes "但如果有了你的幫助";
		mes "我可以影響整個現今的鍊金術師界";
		mes "幫助他們更加進步、完善";
		mes "我求求你, 給我這個機會...";
		mes "";
		close;
	}
	mes "[煉金術師的靈魂]";
	mes "...";
	close;
}

job_soul,1,5,0	script	Timer#link3	111,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	set getvariableofnpc(.SoulLinkerTest,"小孩子#link1"),0;
	end;

OnTimer60000:
OnTimer120000:
	if (getmapusers("job_soul") == 0) {
		stopnpctimer;
		set getvariableofnpc(.SoulLinkerTest,"小孩子#link1"),0;
	}
	end;

OnTimer180000:
OnTimer181000:
OnTimer182000:
	mapwarp "job_soul","morocc",157,47;
	end;

OnTimer183000:
	mapwarp "job_soul","morocc",157,47;
	set getvariableofnpc(.SoulLinkerTest,"小孩子#link1"),0;
	stopnpctimer;
}

//============================================================ 
// Old changelog
//============================================================ 
//= A temp Soul Linker Job Changer based on the kRO quest.
//= Quest info from RagnaInfo. Sprites from kRO screenshots
//= 1.0	Optimized and updated [Lupus]
//= 1.1 Fixed NPC names according to iRO [Lupus]
//============================================================ 