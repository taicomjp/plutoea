//===== rAthena Script ======================================= 
//= 皇家禁衛隊 轉職任務
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from 十字軍 / 聖殿十字軍 -> 皇家禁衛隊.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [brAthena]
//= 1.1 Updated script. [Euphy]
//= 1.2 Added GM management function. [Euphy]
//= 1.3 Added VIP features. [Euphy]
//= Traditional Chinese editted [Yanpo]
//============================================================

prt_castle,48,161,3	script	中年紳士#rg	57,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- Your pack is too heavy. -";
		mes "- Lighten your body first -";
		mes "- and please try again. -";
		close;
	}
	if (Class == Job_Royal_Guard || Class == Job_Royal_Guard_T || Class == Job_Baby_Guard) {
		mes "[中年紳士]";
		mes "Uh? What am I doing? I came here for an errand...";
		next;
		mes "^000099Maybe my feeling has moved to another body.^000000";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "[中年紳士]";
		mes "Oh you are so young. Keep working hard.";
		close;
	}
	if (checkriding() || checkcart() || checkfalcon()) {
		mes "[中年紳士]";
		mes "You are dragging something. If you're riding something you better dismount it before proceeding.";
		close;
	}
	if (BaseJob != Job_Crusader) {
		mes "[中年紳士]";
		mes "I don't have anything to say to you. Find another person.";
		close;
	}
	if (job_royal == 0) {
		mes "[中年紳士]";
		mes "What was I saying... (mumble) the remaining segment is in Glast Heim around 13.......";
		next;
		mes "[中年紳士]";
		mes "嗯?";
		mes "找我有事嗎?";
		next;
		select("我好像還是第一次在這裡見到你");
		mes "[中年紳士]";
		mes "Hehehe, don't worry about me.";
		next;
		mes "[中年紳士]";
		mes "If you don't have business with me, just keep going my friend.";
		next;
		select("You don't look like a Knight...");
		mes "[中年紳士]";
		mes "You're a curious one. Who are you? What do you want?";
		next;
		select("Just looking around!:This is my office!:Who am I? Who are you?");
		mes "[中年紳士]";
		mes "Do you want to argue with me? Do you know who I am?";
		next;
		if(select("Not really, who are you?:Sorry, I won't hurt your feelings.") == 2) {
			mes "["+strcharinfo(0)+"]";
			mes "Sorry. I had to be sure. I didn't know that you are from the royal palace.";
			next;
			mes "[中年紳士]";
			mes "That's enough. Don't make this mistake again.";
			close;
		}
		mes "["+strcharinfo(0)+"]";
		mes "I'm a member here and have the authority to find out what your business is here.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "You are a stranger and I'm not sure if you belong here.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "If you tell me your identity, I will apologize.";
		next;
		mes "[中年紳士]";
		mes "You are so persistent.";
		mes "If you really want, I will introduce myself.";
		next;
		mes "[中年紳士]";
		mes "I am 哈因理希.";
		mes "I'm collecting information about King Schmidtz's trace which has disappeared.";
		next;
		select("Schmidzt's trace? Is it a treasure?");
		mes "[哈因理希]";
		mes "Most archaeologists are eager to get treasure. I'm looking for traces of the ^000099皇家禁衛隊^000000 and their sons who belong to the Schmidtz line.";
		next;
		select("皇家禁衛隊?");
		mes "[哈因理希]";
		mes "Sounds interesting doesn't it? I can't really say but their history and files are under strict security.";
		next;
		select("Can't you tell me?");
		mes "[哈因理希]";
		mes "I can't really talk about the 皇家禁衛隊 to just anyone who asks about them.";
		next;
		mes "[哈因理希]";
		mes "BUT!! I want to believe you...";
		next;
		switch(select("I want to know more.:Stop talking.")) {
		case 1:
			if (getserverdef(VAR_VIP_SCRIPT)) {
				callfunc "VIP_Third_Class";
				set job_royal,1;
				setquest 12090;
				next;
			}
			mes "[哈因理希]";
			mes "You are impetuous.";
			next;
			mes "[哈因理希]";
			mes "It's not a simple story to tell a person like you. Go back.";
			set job_royal,1;
			setquest 12090;
			close;
		case 2:
			mes "[哈因理希]";
			mes "Good idea.";
			next;
			mes "[哈因理希]";
			mes "Just forget it and let your mind do more worthy things.";
			close;
		}
	}
	mes "[哈因理希]";
	if (job_royal == 1) {
		mes "[哈因理希]";
		mes "You are persistent friend.";
		mes "Do you have more things to talk to me about?";
		next;
		select("If you can't trust me, test me pal.");
		mes "[哈因理希]";
		mes "Test?";
		mes "Did you say the test? Huhu!";
		next;
		mes "[哈因理希]";
		mes "Friend, you have shown your interest about my job, are you sure? Or are you kidding me?";
		next;
		select("I'm serious!");
		mes "[哈因理希]";
		mes "Really?";
		mes "Good. You might have the wrong idea about what a 皇家禁衛隊 is. So, in your opinion, what do you think is the key to being a 皇家禁衛隊?";
		next;
		select("Decision and Bravery:Calm Judgment:Sharp wisdom:Benevolent spirit");
		mes "[哈因理希]";
		mes "Good choice.";
		mes "Actually, the other 3 are also needed to have the 皇家禁衛隊 spirit, friend.";
		next;
		mes "[哈因理希]";
		mes "But according to research documents about the 皇家禁衛隊, the best belief is 'asceticism and endurance'.";
		next;
		mes "[哈因理希]";
		mes "Did you get it?";
		mes "Asceticism and Endurance young one. It is a bit different from what you answered.";
		next;
		mes "[哈因理希]";
		mes "You seem to have a large interest about this story but I can't tell you more.";
		next;
		mes "[哈因理希]";
		mes "Only those who have qualification can hear the 皇家禁衛隊 history.";
		next;
		select("What can I do to hear that story?");
		mes "^000099哈因理希 has an agonized look on his face. Then he looked like he fell asleep. Finally he says with reckless abandon.^000000";
		next;
		mes "[哈因理希]";
		mes "You are too stubborn.";
		mes "If so I will test you to see your asceticism and endurance.";
		next;
		mes "[哈因理希]";
		mes "Long ago King Schmidtz gave a title to a brave soldier who come back with 100 Sacred Marks.";
		next;
		mes "[哈因理希]";
		mes "你想繼續聽我的故事，即使不能做到那個程度，至少必須讓我看到你是值得信任的。不是嗎?";
		next;
		if(select("這個要求好像太勉強了:可以試一試") == 1) {
			mes "[哈因理希]";
			mes "Good idea, it would better to find something more worthy.";
			close;
		}
		mes "[哈因理希]";
		mes "Ok, let's do it this way. Actually it's too difficult to collect 100 Sacred Marks, just show your ambition by collecting 100 燃燒心臟s.";
		next;
		mes "[哈因理希]";
		mes "If you bring them I can tell you more of the story. That means you show your endurance to me.";
		next;
		mes "[哈因理希]";
		mes "別忘了，你要交給我100個燃燒的心臟。";
		set job_royal,2;
		changequest 12090,12091;
		close;
	} else if (job_royal == 2) {
		if (countitem(7097) < 100) {
			mes "別忘了，你要交給我100個燃燒的心臟。";
			close;
		}
		mes "Did you get all of them?";
		mes "Most people just ignore me or give up the mission.";
		next;
		mes "[哈因理希]";
		mes "Wait while I arrange the stuff that you brought.";
		delitem 7097,100; //Burning_Heart
		set job_royal,3;
		changequest 12091,12092;
		close;
	} else if (job_royal == 3) {
		mes "......";
		mes "很好，我看到了你的真心，我想我可以再告訴你一些事情。";
		next;
		select("請你盡量講詳細一些");
		mes "[哈因理希]";
		mes "The best members of the Crusader and Paladin classes can gain the qualification of the 皇家禁衛隊.";
		next;
		mes "[哈因理希]";
		mes "They are agents of the divine order and judge with swift punishment.";
		next;
		mes "[哈因理希]";
		mes "We still have many opinions about how they could have survived. Surely someone had to have believed that their history was not false.";
		next;
		select("Are there still documents?");
		mes "[哈因理希]";
		mes "Hmm, documents you say...";
		next;
		mes "[哈因理希]";
		mes "Unfortunately after King Schmidtz disappeared, traces of the 皇家禁衛隊 disappeared.";
		next;
		mes "[哈因理希]";
		mes "Their history, belief, vision and fighting skill seemingly disappeared without any of the documents being saved.";
		next;
		mes "[哈因理希]";
		mes "It's like it disappeared with the king.";
		next;
		select("So the reason of finding their traces...");
		mes "[哈因理希]";
		mes "I was worried about past 皇家禁衛隊s that might not have been able to pass down their history.";
		next;
		mes "[哈因理希]";
		mes "The mysterious power... can't appear anymore in the world. How disappointing is it~!";
		next;
		mes "[哈因理希]";
		mes "That's why I'm concentrating to revive their job.";
		next;
		select("How much progress have you made?");
		mes "[哈因理希]";
		mes "Fortunately, I excavated most of the treasures and interpreted them. I just have one trace left to find.";
		next;
		select("The rest of the traces?");
		mes "[哈因理希]";
		mes "The 皇家禁衛隊 carved the mark of King Schmidtz on their clothes. The trace of this custom is the last clue.";
		next;
		mes "[哈因理希]";
		mes "According to reports from other cities the last trace possibly exists in a monastery inside of Glass Heim.";
		next;
		mes "[哈因理希]";
		mes "But it is too dangerous for me there so I almost gave up.";
		next;
		mes "[哈因理希]";
		mes "為什麼那裡會留有他們的痕跡，現在眾說紛紜，所以我沒辦法告訴你。";
		next;
		if(select("真有趣，我能去看一看嗎?:看來真的很危險") == 2) {
			mes "[哈因理希]";
			mes "Good idea, that's useless work to explore there.";
			close;
		}
		mes "[哈因理希]";
		mes "Are you kidding?";
		mes "You shouldn't treat it like it's just some normal chore.";
		next;
		select("I'm not kidding. I'm serious.");
		mes "[哈因理希]";
		mes "If you say it like that I don't have any more options.";
		next;
		mes "[哈因理希]";
		mes "I don't know if it will be helpful or not. I will write an explanation in detail.";
		next;
		mes "^000099Here's the last trace of the 皇家禁衛隊. Open the quest window and check the position.^000000";
		set job_royal,4;
		changequest 12092,12093;
		close;
	} else if (job_royal == 4) {
		if (countitem(6274) == 0) {
			mes "Our staff has already left to find the last trace, so don't work too hard.";
			close;
		}
		mes "No, it's not the correct one that I want.";
		next;
		mes "[哈因理希]";
		mes "You surprised me. How did you get it? So far no one could get it.";
		next;
		mes "[哈因理希]";
		mes "Thank you so much.";
		mes "Thanks to your effort I can find the last clue. How amazing~!";
		next;
		mes "^000099哈因理希 hesitated and brought an old shield from the side of the office.^000000";
		next;
		mes "[哈因理希]";
		mes "Our staff excavated it. They assumed that King Schmidtz used it. But it wasn't used by him.";
		next;
		select("Sure it's wasn't used by the King?");
		mes "[哈因理希]";
		mes "I can't be entirely sure.";
		mes "Maybe you are right but, according to when we did the initial identification we can be sure it wasn't used.";
		next;
		mes "[哈因理希]";
		mes "But it's an old treasure so, somehow it will be useful. I might know how to use it.";
		next;
		select("Can I keep it?");
		mes "[哈因理希]";
		mes "Of course.";
		mes "Someday it might be useful to you.";
		next;
		mes "^000099You get the shield from 哈因理希. There are some letters carved on the inside of the shield.^000000 ";
		next;
		mes "^000099If you want to see the letters, please check the quest window.^000000";
		next;
		mes "[哈因理希]";
		mes "好了~~那麼，";
		mes "我去分析一下你拿回來的衣服了。我要去忙上一陣子了!";
		delitem 6274,1; //Saint_Cloth_Piece
		getitem 6275,1; //King_Shield
		set job_royal,5;
		changequest 12093,12094;
		close;
	} else if (job_royal == 5) {
		if (countitem(6275) == 0) {
			mes "What? Did you lose the shield? What is the one on your back?";
			getitem 6275,1; //King_Shield
			close;
		}
		mes "Thanks to your help I can finish the excavation work. Thanks a lot.";
		close;
	}
}

glast_01,240,366,5	script	施密特的思念	844,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- Your pack is too heavy. -";
		mes "- Lighten your body first -";
		mes "- and please try again. -";
		close;
	}
	if (ismounting() || checkriding()) {
		mes " Please unequip your riding pet and try again!";
		close;
	}
	if (BaseJob == Job_Crusader && job_royal == 5) {
		if (countitem(6275) == 0) {
			mes "^000099You seem to have lost the shield. Go back to 哈因理希 and ask for help^000000.";
			close;
		}
		mes "[施密特的思念]";
		mes "你是誰?......";
		next;
		select("我把你的遺物拿來了。");
		mes "[施密特的思念]";
		mes "我的遺物?";
		next;
		mes "[施密特的思念]";
		mes "Too bad. Here is one more friend who has been cheated by 哈因理希.";
		next;
		select("Do you know 哈因理希?");
		mes "[施密特的思念]";
		mes "He was my faithful adjutant. And he has continued to be so for hundreds of years.";
		next;
		select("How has he lived for hundreds...");
		mes "[施密特的思念]";
		mes "Maybe his body is just a cast. He found a way to move from body to body in order to stay alive.";
		next;
		mes "[施密特的思念]";
		mes "He doesn't want to accept my death and wanders the world, continuing to find 皇家禁衛隊s.";
		next;
		select("What's the meaning of this shield?");
		mes "[施密特的思念]";
		mes "The shield from 哈因理希 is our contract before I died.";
		next;
		mes "[施密特的思念]";
		mes "哈因理希 was charged with finding the proper people to become 皇家禁衛隊s, the shield was to be the contract.";
		next;
		mes "[施密特的思念]";
		mes "To him, this was the last order that I gave to him.";
		next;
		mes "[施密特的思念]";
		mes "Now he doesn't need to do it anymore, but will remain as a fool whom can't forget his King's final order.";
		next;
		select("So what now? Can I leave?");
		mes "[施密特的思念]";
		mes "Sorry but I can't send you now.";
		next;
		mes "[施密特的思念]";
		mes "This is a contract with 哈因理希 so I have to keep it. You look faithful and brave.";
		next;
		mes "[施密特的思念]";
		mes "Now, it's time to keep the promise with 哈因理希. It's up to you.";
		next;
		mes "[施密特的思念]";
		mes "我打算認可你為皇家禁衛隊的後裔，並把通過秘典傳下來的我們的技能傳授給你。";
		mes ".......你做好心理準備了嗎?";
		next;
		if(select("我已經準備好了:我還沒準備好") == 2) {
			mes "[施密特的思念]";
			mes "Umm, maybe 哈因理希 had wrong judgement.";
			close;
		}
		mes "[施密特的思念]";
		mes "If so, I will proceed to making you a 皇家禁衛隊.";
		next;
		if(select("Great.:Not yet.") == 2) {
			mes "[施密特的思念]";
			mes "Perhaps 哈因理希 had the wrong image of you.";
			close;
		}
		mes "[施密特的思念]";
		if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
			mes "Look at your body before that!";
			next;
			mes "[施密特的思念]";
			mes "Are still sentimentally attached to yourself?";
			mes "You cannot inherit the 皇家禁衛隊!";
			close;
		}
		mes "你的力量.....";
		mes "不要忘記你是為了何種信念，為了什麼而戰的。";
		close2;
		specialeffect2 EF_MAPPILLAR;
		progressbar "FFFF00",7;
		specialeffect2 EF_LORD;
		delitem 6275,1; //King_Shield
		erasequest 12094;
		jobchange roclass(eaclass()|EAJL_THIRD);
		getitem 5757,1; //Schmidt_Helm
		getitem 2795,1; //青蘋果戒指
		end;
	}
	end;
}

gl_church,173,88,0	script	模糊的痕跡#01	844,{
	if (job_royal == 4 && countitem(6274) == 0) {
		progressbar "ffff00",5;
		set .@roy_ran, rand(1,70);
		if (.@roy_ran < 6)
			getitem 6274,1; //Saint_Cloth_Piece
		else if (.@roy_ran > 5 && .@roy_ran < 66)
			getitem 7049,1; //Stone
		else {
			mes "While investigating, the topography breaks. It would be better to dig other places for a while.";
			disablenpc strnpcinfo(0);
			initnpctimer;
			close;
		}
	}
	mes "There is a trace that someone has already dug here.";
	close;
OnTimer60000:
	enablenpc strnpcinfo(0);
	stopnpctimer;
OnTimer1000:
OnTimer20000:
OnTimer40000:
	specialeffect EF_MAPPILLAR;
	end;
	specialeffect EF_MAPPILLAR;
	end;
	specialeffect EF_MAPPILLAR;
	end;
}
gl_church,158,103,0	duplicate(模糊的痕跡#01)	模糊的痕跡#02	844
gl_church,139,90,0	duplicate(模糊的痕跡#01)	模糊的痕跡#03	844
gl_church,126,62,0	duplicate(模糊的痕跡#01)	模糊的痕跡#04	844
gl_church,148,5,0	duplicate(模糊的痕跡#01)	模糊的痕跡#05	844
gl_church,185,72,0	duplicate(模糊的痕跡#01)	模糊的痕跡#06	844
gl_church,185,72,0	duplicate(模糊的痕跡#01)	模糊的痕跡#07	844
gl_church,211,94,0	duplicate(模糊的痕跡#01)	模糊的痕跡#08	844
gl_church,185,125,0	duplicate(模糊的痕跡#01)	模糊的痕跡#09	844
gl_church,143,125,0	duplicate(模糊的痕跡#01)	模糊的痕跡#10	844
gl_church,105,115,0	duplicate(模糊的痕跡#01)	模糊的痕跡#11	844

gl_church,158,100,0	script	#Clue of traces	111,4,4,{
	end;
OnTouch:
	if (job_royal == 4 && countitem(6274) == 0) {
		specialeffect2 EF_FREEZE;
		mes "在這附近感覺到了強大的氣息，最好在周圍仔細搜查一下";
		viewpoint 1,173,88 ,1, 0xFFFF00;
		viewpoint 1,158,103,2, 0xFFFF00;
		viewpoint 1,139,90 ,3, 0xFFFF00;
		viewpoint 1,126,62 ,4, 0xFFFF00;
		viewpoint 1,148,5  ,5, 0xFFFF00;
		viewpoint 1,185,72 ,6, 0xFFFF00;
		viewpoint 1,211,94 ,7, 0xFFFF00;
		viewpoint 1,185,125,8, 0xFFFF00;
		viewpoint 1,143,125,9, 0xFFFF00;
		viewpoint 1,105,115,10,0xFFFF00;
		close;
	}
	end;
}

sec_in02,12,43,3	script	sorcereryal	57,1,1,{
	callfunc "F_GM_NPC";
	switch(select("皇家禁衛隊:盧恩騎士:Sorcerer")) {
	case 1:
		mes "1~5";
		input .@input;
		next;
		if (.@input < 1 || .@input > 5) {
			mes "Set item to adjust the 皇家禁衛隊";
			mes "You can only set a number between 1 to 5.";
		} else {
			set job_royal, .@input;
			mes "Completed";
		}
		close;
	case 2:
		mes "1~24";
		input .@input;
		next;
		if (.@input < 1 || .@input > 24) {
			mes "Set item to adjust the 盧恩騎士";
			mes "You can only set a nbumber between 1 to 24.";
		} else {
			set job_rune_edq, .@input;
			mes "Completed";
		}
		close;
	case 3:
		mes "1~5";
		input .@input;
		next;
		if (.@input < 1 || .@input > 5) {
			mes "Set item to adjust the Sorcerer";
			mes "You can only set a number between 1 to 5.";
		} else {
			set job_soc, .@input;
			mes "Completed";
		}
		close;
	}
}
