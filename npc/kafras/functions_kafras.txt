// 卡普拉函式庫
// 
// 中文化作者:		Dino9021
//
// 版權宣告:		本文件由 eAthenaC 論壇提供，腳本結構屬原作者所有，翻譯及修正之版權屬 Dino9021 所有
//			轉載請勿移除任何說明敘述與說明文件，並需註明論壇連結 http://eathena.twbbs.org
// 
// 中文版本編號:	20041119.0.2
//			20050227.0.1	修正卡普拉銀行手續費 1% -> 0.1%
//					變更卡普拉銀行利息由 0.1% ->0.01%
//			20050317.0.1	加入雨檀族村落 汶巴拉傳送資料
//			20050324.0.1	與 eAthena 2.3 對應
//			20050412.0.1	修改卡普拉銀行存款輸入金額提示訊息，超過一千萬以一千萬計
//			20050420.0.1	與 eAthena 2.4 對應
//
// 增加內容:		朱諾傳送服務項目、卡普拉銀行(可設定是否要計算利息，預設為否)
//
// 注意事項:		F_MercKafra 函式另外寫在商人轉職腳本中
//
//			以下函式庫包括所有卡普拉 NPC 腳本中儲存重生點、倉庫功能、
//			手推車租用、卡普拉傳送以及卡普拉使用卷等功能.
//
//			arg(0): 函式攜帶參數一表示選擇使用哪一個歡迎詞
//			arg(1): 函式攜帶參數二表示選擇使用哪一組選擇項目
//			arg(2): 函式攜帶參數三表示是否使用其他卡普拉服務資料
//
//============================================================

//===== eAthena Script ======================================= 
//= Kafra Functions
//===== By: ================================================== 
//= eAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 5.9
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: ================================= 
//= v1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//= Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=	to block Kafra Password, read comments at 350 line
//=	Note: You can change '1337' value to another to raise password protection
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=	  Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=	Need all coords for all guides.. and somehow pass their coords to kafra.
//=	Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=	Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=	Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=	Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//============================================================ 


// Main Function ===========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables

	//Display Kafra Welcome Message
	switch(getarg(0)){
		//Default message (obsolete)
		default:
		case 0:
			mes "[卡普拉服務人員]";
			mes "歡迎光臨，卡普拉公司隨時為您們服務，";
			break;

		//Niflheim specific message
		case 1:
			mes "[卡普拉服務人員]";
			mes "歡迎... 來到... ";
			mes "卡普拉公司...";
			mes "我們... 到死... 都會繼續為您服務..."; 
			break;

		//Guild Castle Kafra message
		case 2:
			set @GID,getcharid(2);
			mes "[卡普拉服務人員]";
			mes "歡迎, ^5533FF" + GetGuildName(@GID) + "^000000 成員.  我們將和你一起工作";
			break;

		//Amatsu specific message (obsolete)
		case 3:
			mes "[卡普拉服務人員]";
			mes "你重遠方來到此地";
			mes "學習這裡的文化嗎";
			mes "或者你是來觀光的 ?";
			mes "無論是什麼，為什麼不多留一會?";
			mes "這裡的空氣充滿著";
			mes "野花的原始氣息.";
			next;
			mes "[卡普拉服務人員]";
			mes "別擔心. 就算是在國外";
			mes "卡普拉公司也是";
			mes "需要什麼服務嗎?";
			break;

		//Louyang and Ayothaya specific message (obsolete)
		case 4:
			mes "[卡普拉服務人員]";
			mes "有我們的卡普拉服務員在";
			mes "無論你在哪裡就會感覺在";
			mes "家裡一樣";
			break;

		//NPC has it's own welcome message. (Display nothing)
		case 5:
			break;
	}
	next;

	M_Menu:
		cleararray @K_Menu0$[0],"",7;
	switch(getarg(1)){
		// Save and Storage only
		case 1:
			setarray @K_Menu0$[0],"儲存服務","倉庫服務","總結";
			break;

		// Storage only
		case 2:
			setarray @K_Menu0$[0],"倉庫服務","總結";
			break;

		// No Teleport (Common)
		case 3: 
			setarray @K_Menu0$[0],"儲存服務","倉庫服務","手推車服務","指引","其他資訊","總結";
			break;

		// Case 4 is Einbroch no tele message.

		// No save, or teleport. (Common)
		case 5:
			setarray @K_Menu0$[0],"倉庫服務","手推車服務","其他資訊","指引","總結";
			break;

		// Storage and Check Other Information only.
		case 6:
			setarray @K_Menu0$[0],"倉庫服務","指引","其他資訊","總結";
			break;

		// Save, Storage, and Pushcart only (Kafra Warehouse)
		case 7:
			setarray @K_Menu0$[0],"儲存服務","倉庫服務","手推車服務","總結";
			break;

		// Common Kafra Employee
		default:
			setarray @K_Menu0$[0],"儲存服務","倉庫服務","傳送服務","手推車服務","卡普拉銀行","指引","其他資訊","總結";
			break;
	}

	// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
	if(getarg(0)==2) {
		cleararray @K_Menu0$[0],"",7;
		setarray @K_Menu0$[0],"倉庫服務","工會倉庫服務","手推車服務","傳送服務","總結";
	}
	menu 	@K_Menu0$[0],K_Menu0,@K_Menu0$[1],K_Menu1,@K_Menu0$[2],K_Menu2,
		@K_Menu0$[3],K_Menu3,@K_Menu0$[4],K_Menu4,@K_Menu0$[5],K_Menu5,
		@K_Menu0$[6],K_Menu6,@K_Menu0$[7],K_Menu7;
	K_Menu0:
		set @num,0;
		goto K_Menuf;
	K_Menu1:
		set @num,1;
		goto K_Menuf;
	K_Menu2:
		set @num,2;
		goto K_Menuf;
	K_Menu3:
		set @num,3;
		goto K_Menuf;
	K_Menu4:
		set @num,4;
		goto K_Menuf;
	K_Menu5:
		set @num,5;
		goto K_Menuf;
	K_Menu6:
		set @num,6;
		goto K_Menuf;
	K_Menu7:
		set @num,7;
	K_Menuf:
		if (@K_Menu0$[@num] == "儲存服務") return;
		if (@K_Menu0$[@num] == "倉庫服務"){
			// Do not charge for Guild Storage
			if(getarg(0) == 2) callfunc "F_KafStor",2,0,0;
			else callfunc "F_KafStor",0,getarg(3),getarg(0);
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "傳送服務"){
			// Display Einbroch "No Teleport Service" notice.
			if(getarg(1) == 4){
				mes "[卡普拉服務人員]";
				mes "我們卡普拉服務在秀發茲發德總公司，";
				mes "因為和秀發茲發德 共和國";
				mes "的協定，";
				mes "'^ff0000在秀發茲發德內限制傳送服務^000000'";
				mes "的關係，";
				mes "目前不做任何傳送服務的";
				next;
				mes "[卡普拉服務人員]";
				mes "想要移動到其它區域時，";
				mes "請使用飛空艇吧";
				next;
				goto M_Menu;
			}
			callfunc "F_KafTele",getarg(0);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "手推車服務"){
			if(callfunc("F_KafCart",getarg(0),getarg(4)) == 1) next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "指引"){
			callfunc "F_KafGuide";
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "其他資訊"){
			callfunc "F_KafInfo",getarg(2);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "總結"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		}
		if (@K_Menu0$[@num] == "工會倉庫服務"){
			callfunc "F_KafStor",1,0;
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "卡普拉銀行"){
			callfunc "F_KafraBank";
			next;
			goto M_Menu;
		}

}


// Storage Function =======================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if(getarg(0) == 1){
		if(guildopenstorage(0) == 1){
			mes "[卡普拉服務人員]";
			mes "對不起，公會倉庫已經被某公會成員開啟，";
			mes "同時只能有一人進行存取。";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}

	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(basicskillcheck() && getskilllv(1) < 6){
		mes "[卡普拉服務人員]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-s-ssoooorry,";
			mes "y-you're a-a-aaaa";
			mes "Nooviiice... N-neeeds";
			mes "B-basic sssskill l-level 6...^000000";
			return;
		}
		mes "很抱歉，您無法使用倉庫服務，這至少需要職業等級6以上。";
		return;
	}

	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(getarg(0) != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<getarg(1)){
				mes "[卡普拉服務人員]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "M-more z-zeny...!";
					mes "N-neeed 150... zeny...";
					mes "Ergh! T-taking bl-blood~!^000000";
					return;
				}
				//Standard Message
				mes "很抱歉，您的金錢不足! 這至少需要 "+getarg(1)+" Zeny.";
				return;
			}
			set Zeny, Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + (getarg(1)/5);
		}
	}
	mes "[卡普拉服務人員]";
	mes "倉庫在您關閉此窗口後將會打開";
	mes "謝謝您使用本公司所提供的服務";

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[卡普拉服務人員]";
			mes "^666666Thank you.. for... using...";
			mes "Thank you.. for... using...";
			mes "Thank you.. for... using...";
			mes "Thank you.. for... using...";
			mes "Thank you.. for... using...^000000";
		}
		// Normal message
		else {
			mes "[卡普拉服務人員]";
			mes "Here, let me open";
			mes "your Storage for you.";
			mes "Thank you for using";
			mes "the Kafra Service.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set

	close2;
	openstorage;
	cutin "", 255;
	end;
}


// Teleport Function ==================================================
function	script	F_KafTele	{
	mes "[卡普拉服務人員]";
	mes "請選擇您想要去的地方.";
	next;

	menu 	@wrpC$[0],M_Wrp0, @wrpC$[1],M_Wrp1, @wrpC$[2],M_Wrp2, @wrpC$[3],M_Wrp3, 
		@wrpC$[4],M_Wrp4, @wrpC$[5],M_Wrp5, @wrpC$[6],M_Wrp6;

	M_Wrp0:
		set @num, 0;
		goto L_Warp;
	M_Wrp1:
		set @num, 1;
		goto L_Warp;
	M_Wrp2:
		set @num, 2;
		goto L_Warp;
	M_Wrp3:
		set @num, 3;
		goto L_Warp;
	M_Wrp4:
		set @num, 4;
		goto L_Warp;
	M_Wrp5:
		set @num, 5;
		goto L_Warp;
	M_Wrp6:
		set @num, 6;

	L_Warp:
		if (@wrpC$[@num] == "Cancel") return;
		// Consume "Free Ticket for Kafra Transportation" if available.
		// Do not consume if in Guild castle
		if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
		else {
			if (Zeny<@wrpP[@num]){
				mes "[卡普拉服務人員]";
				mes "很抱歉,您的金錢不足. 請確認您身上的現金.";
				emotion e_cash;
				close2;
				cutin "", 255;
				end;
			}
			set Zeny, Zeny-@wrpP[@num];
			set RESRVPTS, RESRVPTS + (@wrpP[@num]/16);
		}
		if (@wrpD$[@num] == "艾爾貝塔") warp "alberta.gat", 117, 56;
		if (@wrpD$[@num] == "艾爾帕蘭") warp "aldebaran.gat",143,110;
		if (@wrpD$[@num] == "克魔島") warp "comodo.gat", 207, 144;
		if (@wrpD$[@num] == "依斯魯得島") warp "izlude.gat", 91, 105;
		if (@wrpD$[@num] == "吉芬") warp "geffen.gat", 120, 39;
		if (@wrpD$[@num] == "夢羅克") warp "morocc.gat", 156, 46;
		if (@wrpD$[@num] == "斐揚") warp "payon.gat", 168, 103;
		if (@wrpD$[@num] == "普隆德拉") warp "prontera.gat", 116, 72;
		if (@wrpD$[@num] == "妙勒尼 廢棄礦場") warp "mjolnir_02.gat", 82, 347;
		if (@wrpD$[@num] == "發樂斯 燈塔島") warp "cmd_fild07.gat", 127, 134;
		if (@wrpD$[@num] == "東邊 獸人村落") warp "gef_fild10.gat", 52, 326;
		if (@wrpD$[@num] == "汶巴拉") warp "umbala.gat", 130, 130;
		if (@wrpD$[@num] == "朱諾") warp "yuno.gat", 157, 123;
		cutin "", 255;
		end;
}


// Cart Function ========================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(baseClass != Job_Merchant){
		mes "[卡普拉服務人員]";
		mes "抱歉! 手推車是商人、鐵匠及鍊金術士和超級初心者專用的服務";
		return 1;
	}
	if(getskilllv(39)==0){
		mes "[卡普拉服務人員]";
		mes "抱歉! 您必須先學會 ^0000FF'手推車使用'^000000 技能才可以租用手推車.";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[卡普拉服務人員]";
		mes "唔... 瞧瞧你的屁股後面，那不正是手推車嗎....";
		return 1;
	}

	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[卡普拉服務人員]";
		mes "此服務需要 "+getarg(1)+" Zeny. 您想要租手推車嗎?";
		next;
		menu "手推車服務",-, "總結",M_End;
			if(Zeny<getarg(1)){
				mes "[卡普拉服務人員]";
				mes "很抱歉，您的金錢不足. 這至少需要 "+getarg(1)+" Zeny";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;

M_End:
	return 0;
}


// Pass Function ===============================================================
function	script	F_KafGuide	{
	mes "[卡普拉服務人員]";
	mes "咦...";
	return;
}

// Special Reserve Points Function ===========================================
function	script	F_KafInfo	{

	sM_Menu:
//Uncomment next line to block Kafra Storage Protection
//	if(getarg(0) == 0) menu "點數累積確認",sM_ResChk, "卡普拉服務人員位置分佈",sM_KafLoc, "取消",sM_End;
	if(getarg(0) == 0) menu "點數累積確認",sM_ResChk, "倉庫密碼服務",sM_KafCode, "卡普拉服務人員位置分佈",sM_KafLoc, "總結",sM_End;

	sM_ResChk:
		mes "[卡普拉服務人員]";
		mes "您目前累積的點數共:";
		mes "^0000ff"+RESRVPTS+"^000000 點.";
		next;
		mes "[卡普拉服務人員]";
		mes "別忘了多多使用卡普拉公司所提供的服務來累積點數喔!";
		next;
		mes "[卡普拉服務人員]";
		mes "您可以用這些累積的點數在艾爾帕蘭的卡普拉總公司兌換遊戲中的物品，累積的點數越多，可兌換的物品越多!";
		next;
		if(getarg(0) == 1) return;
		goto sM_Menu;

	sM_KafLoc:
		mes "[卡普拉服務人員]";
		mes "在小地圖上閃爍的顯示位置是這個城市中所有卡普拉職員的所在位置";
		viewpoint 1,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 1,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 1,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 1,@viewpX[3],@viewpY[3],4,0xFF00FF;
		next;
		viewpoint 2,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 2,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 2,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 2,@viewpX[3],@viewpY[3],4,0xFF00FF;
		goto sM_Menu;

	sM_KafCode:
		callfunc("F_SetKafCode");

	sM_End:
		return;
}


// End Function =====================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[卡普拉服務人員]";
	//Save
	if(getarg(1)==1) {
		mes "已經儲存好了, 謝謝您的使用!";
	}

	// Generic End
	else if(getarg(0)==0 || getarg(0)==5) {
		mes "謝謝您使用卡普拉公司所提供的服務，希望很快能再見到您!";
	}

	// Niflheim End
	else if(getarg(0)==1) {
		percentheal 0,- 25;
		mes "以服務和管理業務為目標，我們卡普拉的服務會更加完善，請安心使用，往後的日子裡會盡力幫助玩家，卡普拉公司謝謝您的使用!";
	}

	close2;
	cutin "", 255;
	end;
}

// Check Storage Password Function ====================
function	script	F_CheckKafCode	{
	if(#kafra_code==0) return;
	mes "請輸入你的倉庫密碼:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code-getcharid(3)-1337) {
		dispbottom "密碼錯誤.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// Set / Change / Clear Storage Password Function ====================
function	script	F_SetKafCode	{
	mes "[卡普拉服務人員]";
	if(#kafra_code) {
		mes "你的倉庫目前有密碼保護，你現在要做什麼?";
		next;
		menu "改變密碼 -> 5000z",-,
		"移除密碼 -> 1000z",M_CLEAR,
		"取消",M_END;
	} else {
		mes "卡普拉服務很高興能提供您新服務:";
		mes "額外的倉庫密碼保護功能";
		next;
		menu "設置新密碼 -> 5000z",M_SET,
		"取消",M_END;
	}

	mes "[卡普拉服務人員]";
	mes "首先請輸入您的 ^0000FF舊密碼^000000.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0 || @code != #kafra_code-getcharid(3)-1337) {
		mes "密碼錯誤，你不能設置新密碼.";
		emotion e_hmm;
		goto M_END;
	}
	next;

M_SET:
	mes "[卡普拉服務人員]";
	mes "現在請輸入您的 ^FF0000新密碼^000000 來保護您的倉庫";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "密碼無法變更";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes "[卡普拉服務人員]";
	if(Zeny < 5000) goto L_ZENY;
	set Zeny,Zeny-5000;
	set RESRVPTS, RESRVPTS + (5000/50);

	set #kafra_code,@code+getcharid(3)+1337;
	mes "您已經使用密碼保護您的倉庫";
	mes "感謝您使用卡普拉服務";
	emotion e_thx;
	goto M_END;	

M_CLEAR:
	mes "[卡普拉服務人員]";
	mes "移除密碼前請請輸入你的密碼確認";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "密碼無法移除";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes "[卡普拉服務人員]";
	if(Zeny < 1000) goto L_ZENY;
	set Zeny,Zeny-1000;
	set RESRVPTS, RESRVPTS + (1000/50);
	if(@code == #kafra_code-getcharid(3)-1337) {
		set #kafra_code,0;
		mes "您已經成功移除您的倉庫密碼";
		mes "感謝您使用卡普拉服務";
		emotion e_thx;
	} else {
		mes "錯誤的密碼。我們將不會歸還您 1000z.";
		mes "請下次輸入您的正確密碼";
		emotion e_sry;
	}
	goto M_END;	

L_ZENY:
	mes "你沒有足夠的金錢.";
	emotion e_cash;
M_END:
	close2;
	cutin "",255;
	end;
}

// Basic Password Validation Function ====================
function	script	F_EntKafCode	{
	mes "輸入數字範圍 1000~10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack: Tried to fit storage password.";
	}
	input @code_;
	if(@code_<1000) {
		mes "密碼太短.";
		return 0;
	}
	if(@code_>10000000) {
		mes "密碼太長.";
		return 0;
	}
	return @code_;
}

// Kafra Bank Function ===============================================================
function	script	F_KafraBank	{
	mes "[卡普拉服務人員]";
	mes "您好! "+strcharinfo(0);
	mes "歡迎使用卡普拉銀行!";

	//calculate %
	set $@countbankincomeornot,0;						//設定是否要計算利息: 0=不要/1=要
	if($@countbankincomeornot==0) goto L_NoIncomeToday;
	set @kb_int,(gettime(6)*31)+gettime(5);					//today's number
	set @income,0;
	if (#kafrabank<=0 || #kb_int>=@kb_int) goto L_NoIncomeToday;
	set @income,(#kafrabank/10000)*(@kb_int-#kb_int);			//@income == % of the summ
	set #kb_int,@kb_int;							//reset days timer

	L_NoIncomeToday:

		if(#kafrabank==0) mes "我們可以幫您開一個帳戶";
		if(@income>0) mes "今天的利息總共是 ^135445" + @income + "^000000 zeny.";
		set #kafrabank,#kafrabank+@income;
		if(#kafrabank>0) mes "您的帳戶共有: ^135445" + #kafrabank + "^000000 zeny.";
		mes "請問您需要哪項服務?";
		next;
		if(#kafrabank==0) menu "請幫我開一個銀行帳戶",BANK_IN,"改天再來",B_EXIT2;
		if(#kafrabank>0) menu "存款",BANK_IN,"提款",BANK_OUT,"改天再來",B_EXIT2;

	BANK_IN:
		if(#kafrabank>999999999) goto BANK_Full;
		mes "[卡普拉服務人員]";
		mes "每存入一筆錢我們將收取";
		mes "0.1%的手續費，以維持我們的服務";
		next;
		mes "[卡普拉服務人員]";
		mes "您銀行戶頭裡目前共有";
		mes "^135445" + #kafrabank + "^000000 Zeny";
		mes " ";
		mes "最多還可存進";
		set @more,1000000000-#kafrabank;
		mes "^135445" + @more + "^000000 Zeny";
		next;
	BANK_IN_AGAIN:
		mes "[卡普拉服務人員]";
		mes "每筆存款交易";
		mes "最低限度是 1,000 Zeny";
		mes "最高限度是 10,000,000 Zeny";
		mes "超過 10,000,000 以 10,000,000 計";
		mes "請輸入要儲存的金額";
		mes "輸入 0 離開";
		next;
		input @kafrabank;

		if(@kafrabank<1) goto B_EXIT2;
		if(@kafrabank<1000) goto BANK_IN_AGAIN;
		if(@kafrabank>Zeny) goto BANK_F;
		set @kafrabank2,@kafrabank*1/1000;
		if(#kafrabank+@kafrabank-@kafrabank2>1000000000) goto BANK_Full;
		set Zeny,Zeny-@kafrabank;
		set @kafrabank,@kafrabank-@kafrabank2;
		set #kafrabank,#kafrabank+@kafrabank;
		mes "[卡普拉服務人員]";
		mes "手續費^135445" + @kafrabank2 + "z^000000 已從你存的金額中扣除";
		mes "存入的金額為 ^135445" + @kafrabank + "z^000000.";
		mes " ";
		mes "您銀行戶頭裡目前共有";
		mes "^135445" + #kafrabank + "^000000 Zeny";
		goto sM_End;

	BANK_OUT:
		if(#kafrabank==0) goto BANK_F2;
		mes "[卡普拉服務人員]";
		mes "您銀行戶頭裡目前共有";
		mes "^135445" + #kafrabank + "^000000 Zeny";
		mes " ";
		mes "最多可提領";
		set @more,1000000000-Zeny;
		if(@more>#kafrabank) set @more,#kafrabank;
		mes "^135445" + @more + "^000000 Zeny";
		next;
		input @kafrabank;

		if(@kafrabank<1) goto B_EXIT2;
		if(@kafrabank>#kafrabank) goto BANK_F;
		if(Zeny+@kafrabank>1000000000) goto Zeny_Full;
		set #kafrabank,#kafrabank-@kafrabank;
		set Zeny,Zeny+@kafrabank;
		mes"[卡普拉服務人員]";
		mes "這裡是您提領的 ^135445" + @kafrabank + "z^000000，請在這裡簽名...";
		goto sM_End;

	BANK_F:
		mes "[卡普拉服務人員]";
		mes "您沒有那麼多錢哩!";
		goto sM_End;

	BANK_Full:
		mes "[卡普拉服務人員]";
		mes "存款總金額超過上限!";
		goto sM_End;

	Zeny_Full:
		mes "[卡普拉服務人員]";
		mes "您所欲提領的的金額加上您目前所持有的現金超過上限!";
		goto sM_End;

	BANK_F2:
		mes "[卡普拉服務人員]";
		mes "您並沒有存款在本行喔!";
		goto sM_End;

	B_EXIT2:
		mes "[卡普拉服務人員]";
		mes "這樣啊...那等您需要的時候再來吧!";
		goto sM_End;

	sM_End:
		return;
}

function	script	KAFURALOGIN	{
	L_RELOGIN:
		if (@err > 2) goto L_GETOUT;
		set @temp1,rand(100,909);
		mes "[卡普拉服務人員]";
		mes "為降低外掛對伺服器的侵擾，我們需要驗證才能服務";
		mes "請輸入驗證碼"+@temp1+"，正確後即可使用卡普拉服務";
		mes "您還有"+(3 - @err)+"次嘗試的機會。";
		next;
		input @temp2;
		if (@temp2 != @temp1) goto L_ERR;
		return;
	L_ERR:
		set @err,@err + 1;
		goto L_RELOGIN;
	L_GETOUT:
		mes "[卡普拉服務人員]";
		mes "你連續3次輸入錯誤，禁止使用卡普拉服務。";
		close;
}