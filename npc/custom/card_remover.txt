// Card removal NPC by TyrNemesis^
// DANGEROUS! TODO: Think.. think 8) [Lupus]
// Transelated By Memory.

prt_in.gat,28,73,4	script	Wise Old Woman	78,{

UPGRADEROOT:
	mes "[拆卡老婆婆]";
	mes "你好啊~~年輕人.我可以使用魔法來幫你拆下裝備上的卡片.你喜歡這項服務嗎?";
	next;
	menu "是的.",REMOVEMENU,
	     "你收費多少啊?",REMOVEPRICE,
	     "不,謝謝.",CLOSEOUT;

REMOVEPRICE:
	mes "[神秘婦女]";
	mes "嗯....卸除一件裝備需要支付^4040FF200000z^000000Zeny.";
	mes "一張卡片加收^4040FF25000z^000000Zeny.";
	mes "另外,我需要一些^4040FF星星的角^000000和^4040FF黃色魔力礦石^000000來輔助我.";
	next;
	menu "那幫我卸除吧.",REMOVEMENU,
	     "想想還是算了.",CLOSEOUT;

REMOVEMENU:
	mes "[拆卡老婆婆]";
	mes "想我幫你卸除哪一件裝備呢?";
	next;
	menu "嗯..讓我想想.",CLOSEOUT,
	     getequipname(1),SLOT1,
	     getequipname(2),SLOT2,
	     getequipname(3),SLOT3,
	     getequipname(4),SLOT4,
	     getequipname(5),SLOT5,
	     getequipname(6),SLOT6,
	     getequipname(7),SLOT7,
	     getequipname(8),SLOT8,
	     getequipname(9),SLOT9,
	     getequipname(10),SLOT10;

SLOT1:
	set @part,1;
	goto CARDNUMCHECK;

SLOT2:
	set @part,2;
	goto CARDNUMCHECK;

SLOT3:
	set @part,3;
	goto CARDNUMCHECK;

SLOT4:
	set @part,4;
	goto CARDNUMCHECK;

SLOT5:
	set @part,5;
	goto CARDNUMCHECK;

SLOT6:
	set @part,6;
	goto CARDNUMCHECK;

SLOT7:
	set @part,7;
	goto CARDNUMCHECK;

SLOT8:
	set @part,8;
	goto CARDNUMCHECK;

SLOT9:
	set @part,9;
	goto CARDNUMCHECK;

SLOT10:
	set @part,10;
	goto CARDNUMCHECK;

CARDNUMCHECK:
	if(getequipcardcnt(@part) == 0) goto DENYCARDCOUNT;
	set @cardcount,getequipcardcnt(@part);
	if(@cardcount > 1) goto CARDNUMMULTIMSG;
	mes "[拆卡老婆婆]";
	mes "這件裝備的插槽上已經插上了" + @cardcount + "張卡片. 你必須支付^0000FF" + (200000+(@cardcount * 25000)) + "Zeny^000000和^0000FF星星的角^000000和^0000FF黃色魔力礦石^000000.";
	goto CARDNUMPOSTMSG;
CARDNUMMULTIMSG:
	mes "[拆卡老婆婆]";
	mes "這件裝備的插槽上已經插上了" + @cardcount + "張卡片. 必須支付^0000FF" + (200000+(@cardcount * 25000)) + "Zeny^000000和^0000FF星星的角^000000和^0000FF黃色魔力礦石^000000..";
CARDNUMPOSTMSG:
	next;
	menu "嗯,開始吧.",REMOVECARDWARNING,
	     "我還沒準備好.",CLOSEOUT;

REMOVECARDWARNING:
	mes "[拆卡老婆婆]";
	mes "在開始之前我得先告訴你--使用這種魔法萬一失敗了.能會損壞了卡片或裝備. 我^FF4040不會賠償^000000給你的.如果損壞的話^FF4040你想保留卡片還是裝備^000000呢?";
	next;
	menu "算了...",CLOSEOUT,
	     "裝備.",PRIORITYITEM,
	     "卡片.",PRIORITYCARD;

PRIORITYITEM:
	set @failtype,1;
	goto REMOVECARD;

PRIORITYCARD:
	set @failtype,2;
	goto REMOVECARD;

REMOVECARD:
	mes "[拆卡老婆婆]";
	mes "那我開始了.";
	if((zeny < (200000+(@cardcount * 25000))) || (countitem(1000) < 1) || (countitem(715) < 1)) goto DENYMATERIAL;
	set zeny,zeny - (200000+(@cardcount * 25000));
	delitem 1000,1;
	delitem 715,1;
// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
// First value = Total failure chance (item and cards destroyed)
// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
// Third value = Harmless failure chance (all that's lost is your investment)

	set @failchance,rand(100);
//	if(@failchance < 2) goto FAILREMOVECARD0;
//	if((@failchance < 8) && (@failtype == 1)) goto FAILREMOVECARD1;
//	if((@failchance < 8) && (@failtype == 2)) goto FAILREMOVECARD2;
	if(@failchance < 10) goto FAILREMOVECARD3;
	successremovecards @part;
	next;
	mes "[拆卡老婆婆]";
	mes "大功告成啦. 這是你的卡片和道具.再見.";
	close;

FAILREMOVECARD0:
	failedremovecards @part,0;
	next;
	mes "[拆卡老婆婆]";
	mes "真太糟糕了.卡片和裝備都壞了.";
	close;

FAILREMOVECARD1:
	failedremovecards @part,1;
	next;
	mes "[拆卡老婆婆]";
	mes "最擔心的事情終於發生了.卡片壞掉了,幸好裝備還在.";
	close;

FAILREMOVECARD2:
	failedremovecards @part,2;
	next;
	mes "[拆卡老婆婆]";
	mes "儘管我已經設法將卡片從裝備的插槽中卸除,但還是被損壞了.幸好,裝備還是好的.";
	close;

FAILREMOVECARD3:
	failedremovecards @part,3;
	next;
	mes "[拆卡老婆婆]";
	mes " 幸運哦~裝備和卡片都沒事.";
	close;

DENYCARDCOUNT:
	mes "[拆卡老婆婆]";
	mes "青年人,道具上沒插卡.";
	close;

DENYMATERIAL:
	next;
	mes "[拆卡老婆婆]";
	mes "你沒有把材料帶齊喔.";
	close;

CLOSEOUT:
	mes "[拆卡老婆婆]";
	mes "很好,以後隨時都可以來找我.";
	close;
}
