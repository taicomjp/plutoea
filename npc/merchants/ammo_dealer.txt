//===== eAthena Script =======================================
//= Ammo Dealer
//===== By: ==================================================
//= Playtester
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena
//===== Description: =========================================
//= trades items for spheres
//===== Additional Comments: =================================
//= 1.0 first version [Playtester]
//============================================================
//============================================================ 
//2007-02-09  黑色小貓  中文化
//============================================================

// Ammo Dealer Tony
que_ng.gat,187,156,3	script	子彈商人 東尼	86,{
	mes "[東尼]";
	mes "你好! 我可以為你服務的項目是，";
	mes "^FF0000榴彈槍^000000子彈的製作!";
	next;
	mes "[東尼]";
	mes "製作 30 枚子彈需要的材料是，";
	mes "^FF00001 個強化武器金屬一級^000000，";
	mes "還有 ^FF00001 個強化武器金屬二級^000000!";
	next;
	mes "[東尼]";
	mes "依照你要製作的子彈，";
	mes "我還需要其他的材料!";
	next;
	mes "[東尼]";
	mes "這是子彈的材料清單：";
	mes "火焰子彈 - ^FF00002 個燃燒的心臟^000000";
	mes "雷擊子彈 - ^FF00003 個賽依伐^000000";
	mes "毒物子彈 - ^FF000010 個毒牙^000000";
	mes "暗盲子彈 - ^FF00005 個墨汁^000000";
	mes "冰凍子彈 - ^FF00002 個布堨Z^000000";
	next;
	mes "[東尼]";
	mes "那麼，請問你要製造哪種子彈呢?";
	next;
	menu "火焰子彈",-,"雷擊子彈",S2,"毒物子彈",S3,"暗盲子彈",S4,"冰凍子彈",S5;

	callsub sF_Make,13203,7097,2;
S2:
	callsub sF_Make,13204,7053,3;
S3:
	callsub sF_Make,13205,937,10;
S4:
	callsub sF_Make,13206,1024,5;
S5:
	callsub sF_Make,13207,7054,2;

//Subfunction, getarg(0):created sphere, getarg(1):special material, getarg(2):number needed
sF_Make:
	if( (countitem(1010)<1) || (countitem(1011)<1) || (countitem(getarg(1))<getarg(2)) ) goto L_NoMat;
	mes "[東尼]";
	mes "好的! 那麼請問你需要多少數量呢? 一包 30 顆!";
	next;
	menu "換取最大可交易的數量",M_0, "讓我選擇我要的數量!",M_1, "取消",M_End;

	M_0:
		set @amount, 500;
		if(countitem(1010) < @amount) set @amount,countitem(1010);
		if(countitem(1011) < @amount) set @amount,countitem(1011);
		if(countitem(getarg(1))/getarg(2) < @amount) set @amount,countitem(getarg(1))/getarg(2);
		if(@amount > 0) goto L_Make;
		mes "[東尼]";
		mes "你是在耍我嗎...?";
		close;

	M_1:
		input @amount;
		if(@amount < 1 || @amount > 500) goto M_End;
		if(countitem(1010) < @amount) goto L_NoMat;
		if(countitem(1011) < @amount) goto L_NoMat;
		if(countitem(getarg(1))/getarg(2) < @amount) goto L_NoMat;

	L_Make:
		delitem 1010,@amount;
		delitem 1011,@amount;
		delitem getarg(1),@amount*getarg(2);
		getitem getarg(0),@amount*30;

	M_End:
		mes "[東尼]";
		mes "歡迎再次光臨!";
		close;

	L_NoMat:
		mes "[東尼]";
		mes "很抱歉! 你沒有足夠的材料製作子彈!";
		close;
}
