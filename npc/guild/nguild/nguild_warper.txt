//===== eAthena Script =======================================
//= Novice's Guild Castles War of Emperium Usher NPC
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= eAthena 1+; RO Episode 4+
//===== Description: =========================================
//=
//===== Additional Comments: =================================
//= Now you have access to 4 N Guild castles. They don't have
//= dungeons. And 2nd Classes can't seize these Castles.
//= These new castles need a new strategy. It would bring some
//= fun and live to your game.
//= NOTE: If your Guild Master is 2nd class, then he could 
//=	rule the Castles and gather Treasure Boxes after WoE
//= 1.1 Now 2nd classes can't enter NC place at all
//= 1.2 Restricted access of SG/SL. On warp clear some 
//=	unallowed buffs [Lupus]
//= 1.3 Restricted TK,High classes >=90 BaseLevel,
//= 	updated list of unallowed buffs [Lupus]
//= 1.4 Restricted it to 80 Base Level [Lupus]
//============================================================


prontera.gat,146,163,6	script	初心者城堡	729,{
	mes "[西塔]";
	mes "嗨! 我是新的初心者城堡接待員";
	next;
	if( (Class>=Job_Novice && Class<=Job_Thief)
		|| (Class==Job_Taekwon && BaseLevel<80) || Class==Job_SuperNovice || Class==Job_Super_Baby
		|| (Class>=Job_Baby && Class<=Job_Baby_Thief)
		|| (Class>=Job_Novice_High && Class<=Job_Thief_High && BaseLevel<80)
	) menu "傳送到初心者城堡",M_WARP,"取消",-;

	mes "[西塔]";
	mes "所有二轉職業無法進入初心者城堡";
	mes "基本等級超過80的跆拳少年/少女或進階職業也不行";
	emotion 0;
	close;

M_WARP:
	//remove several unallowed buffs
	sc_end SC_ASSUMPTIO;
	sc_end SC_IMPOSITIO;
	sc_end SC_SUFFRAGIUM;
	sc_end SC_MAGNIFICAT;
	sc_end SC_WEAPONPERFECTION;
	sc_end SC_GOSPEL;
	sc_end SC_BASILICA;
	sc_end SC_MAGICPOWER;
	sc_end SC_MARIONETTE;
	sc_end SC_MARIONETTE2;
	sc_end SC_DEVOTION;
	sc_end SC_SACRIFICE;
	sc_end SC_MAXOVERTHRUST;
	sc_end SC_SPIRIT;
	warp "n_castle.gat",102,93+rand(14);
	close;
}


n_castle.gat,102,107,5	script	Cita	729,{
	mes "[西塔]";
	mes "你好, "+ strcharinfo(0) +". 需要幫忙嗎?";
	next;
	menu "傳送到普隆德拉!",-,"取消",LEnd;
	
		warp "prontera.gat",155,177+rand(5);
		close;
	LEnd:
		mes "[西塔]";
		mes "好的.";
		close;
}